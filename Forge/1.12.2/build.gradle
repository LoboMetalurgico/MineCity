buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

project(':Forge:1.12.2') {
    archivesBaseName = "MineCity-Forge-MC1.12.2"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

minecraft {
    version = "1.12.2-14.23.5.2769"
    runDir = "run"

    clientJvmArgs += "-Dfml.coreMods.load=br.com.gamemods.minecity.forge.mc_1_12_2.core.MineCityColorCoreMod"
    serverJvmArgs += "-Dfml.coreMods.load=br.com.gamemods.minecity.forge.mc_1_12_2.core.MineCityColorCoreMod"
    mappings = "snapshot_20171003"
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven { url = "http://maven.cil.li/" }
    maven { url = "http://maven.ic2.player.to/" }
}

dependencies {
    shade project(':Core')
    shade project(':Forge:Base')
    compile "mysql:mysql-connector-java:6.0.3"
    testCompile fileTree(dir: "run/mods", include: ["*.jar"], exclude:["*Assets*"])
    testCompile fileTree(dir: "run/mods/1.12.2", include: ["*.jar"])
}

jar {
    manifest {
        attributes (
                'FMLCorePlugin': 'br.com.gamemods.minecity.forge.mc_1_12_2.core.MineCityColorCoreMod',
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLAT': 'minecity_at.cfg'
        )
    }

    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            include 'br/**', '*.yml', 'deps.info', 'minecity_*.cfg', 'assets/minecity/**', 'org/mcstats/**'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
